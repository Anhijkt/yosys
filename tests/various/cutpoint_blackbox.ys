read_verilog -specify << EOT
module top(input a, b, output o);
    wire c, d, e;
    bb #(.SOME_PARAM(1)) bb1 (.a (a), .b (b), .o (c));
    bb #(.SOME_PARAM(2)) bb2 (.a (a), .b (b), .o (d));
    wb wb1 (.a (a), .b (b), .o (e));
    some_mod some_inst (.a (c), .b (d), .c (e), .o (o));
endmodule

(* blackbox *)
module bb #( parameter SOME_PARAM=0 ) (input a, b, output o);
assign o = a | b;
specify
	(a => o) = 1;
endspecify
endmodule

(* whitebox *)
module wb(input a, b, output o);
assign o = a ^ b;
endmodule

module some_mod(input a, b, c, output o);
assign o = a & (b | c);
endmodule
EOT

hierarchy -top top
design -save hier

select -assert-count 0 t:$anyseq
select -assert-count 3 =t:?b
cutpoint -blackbox

select -assert-count 3 =t:?b
select -assert-count 2 r:SOME_PARAM
select -assert-count 1 r:SOME_PARAM=1

flatten
select -assert-count 3 t:$anyseq
select -assert-count 3 t:$scopeinfo n:*cutpoint.cc* %i

# cutpoint -blackbox === cutpoint =A:whitebox =A:blackbox %u
#                        (simplified to =A:*box)
design -load hier
cutpoint -blackbox
rename -enumerate -pattern A_% t:$scopeinfo
rename -enumerate -pattern B_% t:$anyseq
rename -enumerate -pattern C_% w:*Anyseq*
design -save gold
select -write cutpoint.gold.sel =*

design -load hier
cutpoint =A:*box
rename -enumerate -pattern A_% t:$scopeinfo
rename -enumerate -pattern B_% t:$anyseq
rename -enumerate -pattern C_% w:*Anyseq*
design -save gate
select -write cutpoint.gate.sel
select -read cutpoint.gold.sel
# nothing in gate but not gold
select -assert-none % %n

design -load gold
select -read cutpoint.gate.sel
# nothing in gold but not gate
select -assert-none % %n

# cutpoint -blackbox -instances !== cutpoint =A:whitebox =A:blackbox %u %C
#                                   (simplified to =A:*box %C)
# because cutpoint -blackbox -instances adds $scopeinfo cells
design -load hier
cutpoint -blackbox -instances
rename -enumerate -pattern A_% t:$scopeinfo
rename -enumerate -pattern B_% t:$anyseq
rename -enumerate -pattern C_% w:*Anyseq*
design -save gold
select -write cutpoint.gold.sel =*

design -load hier
cutpoint =A:*box %C
rename -enumerate -pattern A_% t:$scopeinfo
rename -enumerate -pattern B_% t:$anyseq
rename -enumerate -pattern C_% w:*Anyseq*
design -save gate
select -write cutpoint.gate.sel
select -read cutpoint.gold.sel
# nothing in gate but not gold
select -assert-none % %n

design -load gold
select -read cutpoint.gate.sel
# 3 $scopeinfo in gold but not gate
select -assert-count 3 % %n
select -assert-count 3 t:$scopeinfo
